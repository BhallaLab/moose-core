////////////////////////////////////////////////////////////////
// This script tests a superficial lowthreshold spiking neuron
// from the NEURON port of Traub et al, 2005.
// It simulates 200 ms of real time and applies a 0.3 nA
// current injection for 50 ms from the start of simulation.
// The Vm and [Ca2+] respectively of the presynaptic and 
// the soma compartment are stored in the data directory.
// Please see README file for references.
//
// Author: Subhasis Ray / NCBS, Bangalore/ October 2009
////////////////////////////////////////////////////////////////
load_file("nrngui.hoc")
load_file("hoc/traubcon.hoc")


load_file("cells/supLTS_template.hoc")
strdef tstr
PI=3.141592
FARADAY=96485
secondorder=2

celsius=30
v_init = -65.0

define_shape()

objref cell, stim1, stim2


proc runcell(){ local i, t1, a1, t2, a2 localobj vvec, tvec, f, g, caconc, ikvec
    print "--------------------------------------------------------------"
    print "|    NEURON Simulation of supLTS cell                        |"
    print "--------------------------------------------------------------"
    tstop = $1
    t1 = $2
    t2 = $3
    cell = new supLTS()
    access cell.comp[1]
    f = new File()
    tvec = new Vector()
    vvec = new Vector()
    caconc = new Vector()
    
    tvec.record(&t)
    vvec.record(&cell.comp[cell.presyn_comp].v(0.5))
    caconc.record(&cell.comp[1].cai(0.5))
    stim1 = new IClamp(0.5) 
    stim1.amp = 0.3
    stim1.del = t1
    stim1.dur = t2
    printf("Run for: %g ms, inject %g nA current at %g ms for %g ms\n", tstop, stim1.amp, stim1.del, stim1.dur)    
    system("date '+%s.0 + %Ne-9'") // print the current time seconds
    run()
    system("date '+%s.0 + %Ne-9'") // print the current time seconds
    f.wopen("data/Vm_supLTS.plot")
    for ii = 0, vvec.size() - 1{
    	f.printf("%g %g\n", tvec.x(ii), vvec.x(ii))
    }
    f.close()
    f.wopen("data/Ca_supLTS.plot")
    for ii = 0, caconc.size() - 1{
    	f.printf("%g %g\n", tvec.x(ii), caconc.x(ii))
    }
    f.close()
    print "--------------------------------------------------------------"
    print "|    NEURON Simulation of supLTS cell finished                |"
    print "--------------------------------------------------------------"
   
}

proc finit0(){
    print "finit0"
    forsec cell.all { v = -65. }
}

runcell(200., 50., 50.0)
quit()
