// Written by Subhasis Ray @ NCBS, March, 2010
//
// This code creates two compartments soma_a and soma_b,
// and creates an NMDA channel from soma_a to soma_b.
// Then soma_a is stimulated using a current clamp 
// and the membrane potential of soma_b is recorded.
// It requires the NMDA mechanism (traub_nmda.hoc) which 
// is part of the NEURON version  of Single Column 
// Thalamocortical Model by Traub et al, 2005.
objref netcon, conobj, syn
objectvar vecT, vecV_a, vecV_b, stim1, vecG_a, vecG_b, outfile
outfile = new File()
vecV_a = new Vector()
vecV_b = new Vector()
vecG_a = new Vector()
vecG_b = new Vector()
vecT = new Vector()

FARADAY=96485
PI=3.141592
secondorder=2
dt=1e-3
celsius=30
v_init=-65

create soma_a
access soma_a
soma_a{
    nseg = 1
    diam = 15
    L = 20
    
    insert pas
    e_pas = -65
    g_pas = 2e-5
    Ra = 250.0
    cm = 0.9
    
    stim1 = new IClamp(0.5)
    stim1.amp = 0.1
    stim1.dur = 20
    stim1.del = 20
}

create soma_b
access soma_b
soma_b{
    nseg = 1
    diam = 15
    L = 20
    insert pas
    e_pas = -65
    g_pas = 2e-5
    Ra = 250.0
    cm = 0.9
    
    syn = new NMDA(0.5)
    syn.tau = 130.5 // ms
    
}
// new NetCon(&source_v, synapse, threshold, delay, weight)
soma_a netcon = new NetCon(&v(1), syn, 0.0, 0.05, 0.25e-3)
// netcon.weight = 0.025e-3 // uS
// netcon.delay = 0.05 // ms
// netcon.threshold = 0.0

tstop = 50
vecV_a.record(&soma_a.v(0.5))
vecG_b.record(&syn.g)
vecV_b.record(&soma_b.v(0.5))
vecT.record(&t)
run()
outfile.wopen("nmda_neuron.plot")
for ii = 0, vecV_a.size() - 1 {
	outfile.printf("%g\t%g\t%g\t%g\n", vecT.x(ii), vecV_a.x(ii), vecV_b.x(ii), vecG_b.x(ii))
}
outfile.close()

